import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'com.adarshr.test-logger' version '3.1.0'
}

apply plugin: "java-library"
apply plugin: "java-test-fixtures"
apply plugin: "groovy"

dependencies {

    api resolveProject(":manager")

    implementation "org.codehaus.groovy:groovy:$groovyVersion"
    testFixturesApi platform("org.spockframework:spock-bom:$spockVersion")
    testFixturesApi 'org.spockframework:spock-core'

    // optional dependencies for using Spock
    testRuntimeOnly "net.bytebuddy:byte-buddy:$byteBuddyVersion" // allows mocking of classes (in addition to interfaces)
    testRuntimeOnly "org.objenesis:objenesis:$objenesisVersion"      // allows mocking of classes without default constructor (together with ByteBuddy or CGLIB)

    api("com.github.calimero:calimero-server:$calimeroVersion") {
        // Libraries should not depend on a logging implementation and/or alpha versions!
        exclude group: "org.slf4j"
    }
}

tasks.withType(Test) {
    environment("OR_LOGGING_CONFIG_FILE", "test/src/logging-test.properties")
}

test {
    workingDir = findProject(":openremote") != null ? resolveProject("").projectDir : rootProject.projectDir
    useJUnitPlatform()
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

//        afterTest { desc, result ->
//            logger.quiet "${desc.className} > ${desc.name} took: ${(result.endTime - result.startTime)}ms"
//        }

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
